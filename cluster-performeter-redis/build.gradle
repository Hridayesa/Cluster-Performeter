group 'org.vs.performeter'
version '1.0-SNAPSHOT'

apply plugin: 'application'

dependencies {
    compile project(':cluster-performeter-core')
    compile 'org.springframework.data:spring-data-redis:1.7.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-redis:1.4.2.RELEASE'
}

// Генерация дистрибутива
// два набора запускающих скриптов
mainClassName = "org.vs.performeter.redis.tester.TesterMain"
applicationDefaultJvmArgs = ['-Dhazelcast.config=../config/hazelcast.xml', '-Duser.language=ru', '-Duser.region=RU', '-Dfile.encoding=UTF-8']

task moreStartScripts(type: CreateStartScripts) {
    mainClassName = "org.vs.performeter.redis.orchestrator.RunnerMain"
    applicationName = "analizer"
    defaultJvmOpts = ['-Dhazelcast.config=../config/hazelcast.xml', '-Duser.language=ru', '-Duser.region=RU', '-Dfile.encoding=UTF-8']
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(moreStartScripts)
    fileMode = 0755
}

distributions {
    main {
        contents {
            from("src/main/resources") {
                include 'app.properties'
                include 'hazelcast.xml'
                into "config"
            }
        }
    }
}
